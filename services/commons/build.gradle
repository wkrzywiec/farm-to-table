plugins {
    id 'idea'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'groovy'
    id 'jacoco'
    id "org.barfuin.gradle.jacocolog" version "3.0.0-RC2"
}

group = 'io.wkrzywiec'
version = '0.0.1-SNAPSHOT'
description = 'food-delivery common libraries'
sourceCompatibility = '21'

bootJar {
    enabled = false
}

jar {
    enabled = true
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }

    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {

    modules {
        module("org.codehaus.groovy:groovy") {
            replacedBy("org.apache.groovy:groovy", "conflicts in current rest-assured version")
        }
        module("org.codehaus.groovy:groovy-json") {
            replacedBy("org.apache.groovy:groovy-json", "conflicts in current rest-assured version")
        }
        module("org.codehaus.groovy:groovy-xml") {
            replacedBy("org.apache.groovy:groovy-xml", "conflicts in current rest-assured version")
        }
    }

    def springBootVersion = '3.4.3'
    def lombokVersion = '1.18.36'
    def spockVersion = '2.4-M5-groovy-4.0'
    def testcontainersVersion = '1.20.6'

    implementation "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation 'com.google.code.gson:gson:2.12.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.3'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.18.3'
    implementation "org.springframework.boot:spring-boot-starter-data-redis:${springBootVersion}"

    implementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    implementation "org.spockframework:spock-core:${spockVersion}"
    implementation "org.spockframework:spock-spring:${spockVersion}"
    implementation 'org.apache.groovy:groovy-all:4.0.26'
    implementation ('io.rest-assured:spring-mock-mvc:5.5.1') { exclude group: 'org.codehaus.groovy' }
    implementation "org.testcontainers:spock:${testcontainersVersion}"
}

test {
    useJUnitPlatform()
}

sourceSets {
    integrationTest {
        groovy.srcDir "$projectDir/src/integration-test/groovy"
        resources.srcDir "$projectDir/src/integration-test/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    useJUnitPlatform()
}

check.dependsOn integrationTest

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn = [
            'test',
            'integrationTest'
    ]
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.0
            }
        }
    }
}
