plugins {
    id 'idea'
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'groovy'
    id 'jacoco'
    id "org.barfuin.gradle.jacocolog" version "3.0.0-RC2"
    id 'org.sonarqube' version "4.2.0.3129"
}

group = 'io.wkrzywiec'
version = '0.0.1-SNAPSHOT'
description = 'farm-to-table common libraries'
sourceCompatibility = '17'

bootJar {
    enabled = false
}

jar {
    enabled = true
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }

    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {

    def springBootVersion = '2.7.1'
    def lombokVersion = '1.18.24'
    def spockVersion = '2.3-groovy-4.0'
    def testcontainersVersion = '1.18.3'

    implementation "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'
    implementation 'org.postgresql:postgresql:42.5.1'
    implementation "org.springframework.boot:spring-boot-starter-jdbc:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-data-redis:${springBootVersion}"

    implementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    implementation "org.spockframework:spock-core:${spockVersion}"
    implementation "org.spockframework:spock-spring:${spockVersion}"
    implementation 'org.apache.groovy:groovy-all:4.0.13'
    implementation 'io.rest-assured:spring-mock-mvc:5.3.1'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.11.5'

    implementation "org.testcontainers:spock:${testcontainersVersion}"
    implementation "org.testcontainers:postgresql:${testcontainersVersion}"

    //this part is needed, because io.rest-assured:spring-mock-mvc depends on old lib for groovy - org.codehaus.groovy:groovy
    modules {
        module("org.codehaus.groovy:groovy") {
            replacedBy("org.apache.groovy:groovy", "conflicts in current rest-assured version")
        }

        module("org.codehaus.groovy:groovy-xml") {
            replacedBy("org.apache.groovy:groovy-xml", "conflicts in current rest-assured version")
        }

        module("org.codehaus.groovy:groovy-json") {
            replacedBy("org.apache.groovy:groovy-json", "conflicts in current rest-assured version")
        }
    }
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn = [
            'test'
    ]
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.0
            }
        }
    }
}

sonarqube {
    properties {
        property 'sonar.projectName', 'commons'
        property "sonar.projectKey", "farm-to-table-app_food"
        property "sonar.organization", "farm-to-table-app"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
