plugins {
	id 'org.jetbrains.kotlin.jvm' version '2.1.10'
	id 'org.jetbrains.kotlin.plugin.spring' version '2.1.10'
}
description = 'farm-to-table ordering domain'

def activeDevProfiles = 'dev'

dependencies {

	def lombokVersion = '1.18.32'
	def spockVersion = '2.4-M4-groovy-4.0'
	def testcontainersVersion = '1.19.7'
	def mapstructVersion = '1.5.5.Final'

	implementation "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
	testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

	implementation project(':commons')
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'

	implementation 'com.github.sonus21:rqueue-spring-boot-starter:3.1.1-RELEASE'

	implementation 'org.postgresql:postgresql:42.7.3'
	implementation 'org.liquibase:liquibase-core:4.27.0'

	implementation "org.mapstruct:mapstruct:${mapstructVersion}"
	implementation 'io.vavr:vavr:0.10.4'
	implementation 'org.apache.commons:commons-lang3:3.14.0'
	implementation 'com.google.code.gson:gson:2.10.1'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.apache.groovy:groovy-all:4.0.21'
	testImplementation "org.spockframework:spock-core:${spockVersion}"
	testImplementation "org.spockframework:spock-spring:${spockVersion}"
	testImplementation 'io.rest-assured:spring-mock-mvc:5.4.0'
	testImplementation "org.testcontainers:spock:${testcontainersVersion}"
	testImplementation "org.testcontainers:postgresql:${testcontainersVersion}"

	testImplementation 'org.awaitility:awaitility:4.2.0'
	testImplementation ('com.github.javafaker:javafaker:1.0.2') { exclude module: 'snakeyaml' }
	testImplementation 'org.yaml:snakeyaml:2.2'

	//this part is needed, because io.rest-assured:spring-mock-mvc depends on old lib for groovy - org.codehaus.groovy:groovy
	modules {
		module("org.codehaus.groovy:groovy") {
			replacedBy("org.apache.groovy:groovy", "conflicts in current rest-assured version")
		}

		module("org.codehaus.groovy:groovy-xml") {
			replacedBy("org.apache.groovy:groovy-xml", "conflicts in current rest-assured version")
		}

		module("org.codehaus.groovy:groovy-json") {
			replacedBy("org.apache.groovy:groovy-json", "conflicts in current rest-assured version")
		}
	}
}

bootRun {
	args = ["--spring.profiles.active=" + activeDevProfiles]
}

sonarqube {
	properties {
		property 'sonar.projectName', 'ordering'
		property "sonar.projectKey", "farm-to-table-app_ordering"
		property "sonar.organization", "farm-to-table-app"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}
